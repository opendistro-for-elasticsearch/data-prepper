sourceSets {
    main {
        resources {
            srcDirs += "../shared-config"
        }
    }
}

apply from: "integrationTest.gradle"

dependencies {
    implementation project(':data-prepper-api')
    implementation project(':data-prepper-plugins')
    implementation project(':data-prepper-plugins:common')
    testImplementation project(':data-prepper-plugins:common').sourceSets.test.output
    implementation "com.fasterxml.jackson.core:jackson-databind:2.12.4"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.12.4"
    implementation "javax.validation:validation-api:2.0.1.Final"
    implementation "org.apache.bval:bval-extras:2.0.5"
    implementation "org.apache.bval:bval-jsr:2.0.5"
    implementation "org.reflections:reflections:0.9.12"
    implementation "io.micrometer:micrometer-core:1.6.5"
    implementation "io.micrometer:micrometer-registry-prometheus:1.6.5"
    implementation "io.micrometer:micrometer-registry-cloudwatch2:1.7.2"
    implementation "software.amazon.awssdk:cloudwatch:2.17.71"
    testImplementation "org.hamcrest:hamcrest:2.2"
    testImplementation "org.mockito:mockito-core:3.11.2"
}

jar {
    dependsOn configurations.runtimeClasspath
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Main-Class': 'com.amazon.dataprepper.DataPrepperExecute')
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule { //in addition to core projects rule
            limit {
                // temporarily lowering this to unblock the release.
                // some builds pass with 0.9 coverage, some fail with 0.89
                minimum = 0.89
            }
        }
    }
}
